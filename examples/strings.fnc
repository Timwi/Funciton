
                ╔════╗           ╔══════════════════════════════════════════════════╗
                ║ 21 ║           ║  String length                                   ║
                ╚═╤══╝  ╓───╖    ╟──────────────────────────────────────────────────╢
         ┌───╖  ┌─┴──╖  ║ ℓ ║    ║  ℓ(s) = (s = 0 | s = −1) ? 0 : succ(ℓ(s >> 21))  ║
    ┌────┤ ℓ ╟──┤ >> ║  ╙─┬─╜    ╚══════════════════════════════════════════════════╝
    │    ╘═══╝  ╘═╤══╝    │      
  ┌─┴────╖        └───────┴──┐
  │ succ ║      ╔═══╗  ┌───╖ │         
  ╘═╤════╝ ┌────╢ 0 ╟──┤ ≠ ╟─┴┐         
    │    ┌─┴─╖  ╚═══╝  ╘═╤═╝  │          ┌─────┐    ╓───╖ 
    └────┤ ? ╟───────────┤    │          │     ├────╢ ‼ ╟───┐ 
         ╘═╤═╝ ╔════╗  ┌─┴─╖  │          │   ┌─┴─╖  ╙───╜   │ 
           │   ║ −1 ╟──┤ ≠ ╟──┘          │   │ ℓ ║          │ 
               ╚════╝  ╘═══╝             │   ╘═╤═╝          │ 
                                         │   ┌─┴─╖  ┌────╖  │ 
                                         │   │ × ╟──┤ << ╟──┘ 
                                         │   ╘═╤═╝  ╘═╤══╝ 
 ╔══════════════════════════════════╗    │   ╔═╧══╗   │ 
 ║  String concatenation            ║   ┌┴┐  ║ 21 ║  ┌┴┐ 
 ╟──────────────────────────────────╢   └┬┘  ╚════╝  └┬┘ 
 ║  ‼(a, b) = (b << (21 × ℓa)) | a  ║    └──────┬─────┘ 
 ╚══════════════════════════════════╝           │



                         ╓─────────╖       ╔═════════════════════════════════════════════════════════╗
                         ║ str→int ║       ║  str→int                                                ║
     ╔═════════╗         ╙────┬────╜       ╟─────────────────────────────────────────────────────────╢
     ║ 2097151 ╟────┬─────────┴─────────┐  ║  • Converts numeric strings to their integer value      ║
     ╚═════════╝   ┌┴┐                  │  ║  • Accepts − (minus) or - (hyphen) for negative         ║
     ╔════╗  ┌───╖ └┬┘ ┌───╖  ╔══════╗  │  ║  • Returns 0 if the string is not an integer            ║
     ║ 45 ╟──┤ ≠ ╟──┴──┤ ≠ ╟──╢ 8722 ║  │  ║  • No tolerance for leading/trailing whitespace         ║
     ╚════╝  ╘═╤═╝     ╘═╤═╝  ╚══════╝  │  ║  • No support for thousands separators                  ║                      
               └────┬────┘            ┌─┴─╖╚═════════════════════════════════════════════════════════╝                      
    ┌───────────────┴─────────────────┤ · ╟──────────────────────┐                                                          
    │  ╔═════════════════════════════╗╘═╤═╝                      │                                                          
    │  ║ str→int(s) =                ║  │  ┌──────────────────┐  │                                                          
    │  ║  let c = s & (1<<21 − 1);   ║  │  │  ╔════╗  ┌────╖  │  │                                                          
    │  ║  let m = c = '-' | c = '−'; ║  └──┤  ║ 21 ╟──┤ >> ╟──┘  │                                                          
    │  ║  let w = m ? s >> 21 : s;   ║     │  ╚════╝  ╘═╤══╝     │                                                          
    │  ║  let t = str→int p(w, 0);   ║     │          ┌─┴─╖      │                                                          
    │  ║  t = -1 ? 0 : m ? ~t : t    ║     └──────────┤ ? ╟──────┘                                                          
    │  ╚═════════════════════════════╝                ╘═╤═╝                                                                 
    └────────┐    ╔═══╗                ┌───────────╖    │                                                                   
             │    ║ 0 ╟────────────────┤ str→int p ╟────┘                                                                   
             │    ╚═╤═╝    ┌──────┐    ╘════╤══════╝
           ┌─┴─╖  ┌─┴─╖  ┌─┴─╖  ╔═╧══╗      │
        ┌──┤ ? ╟──┤ ? ╟──┤ = ║  ║ −1 ║      │
      ┌─┴─╖╘═╤═╝  ╘═╤═╝  ╘═╤═╝  ╚════╝      │
      │ ~ ║  │      │      ├────────────────┘
      ╘═╤═╝  ├─────────────┘
        └────┘

         ╓┬───────────╖
      ┌──╫┘ str→int p ╟────────────────┐
      │  ╙────────────╜  ┌─────────────┴───────────────────┐
      │                  │      ╔═════════╗                │
      │               ┌──┴───┬──╢ 2097151 ║  ╔═══╗  ┌───╖  │  ┌───╖  ╔════╗
   ┌──┴─┐             │     ┌┴┐ ╚═════════╝  ║ 0 ╟──┤ ≠ ╟──┴──┤ ≠ ╟──╢ −1 ║
   │  ┌─┴─╖  ┌───╖  ┌─┴─╖   └┬┘              ╚═══╝  ╘═╤═╝     ╘═╤═╝  ╚════╝
   │  │ × ╟──┤ + ╟──┤ · ╟────┴───────────┐            └────┬────┘
   │  ╘═╤═╝  ╘═╤═╝  ╘═╤═╝ ╔════╗  ┌───╖  │  ┌───╖  ╔════╗  │
   │  ╔═╧══╗   │      │   ║ 48 ╟──┤ ≥ ╟──┴──┤ ≥ ╟──╢ 57 ║ ┌┴┐
   │  ║ 10 ║   │      └┐  ╚════╝  ╘═╤═╝     ╘═╤═╝  ╚════╝ └┬┘
   │  ╚════╝   │       └┐   ┌───┐   └────┬────┘            │
   │  ┌────────┘        └┐  │   ├────────┘    ┌────────────┘
   │  │  ╔════╗  ┌────╖  │  └─┬─┘             │  ╔═══════════════════════════════════════════╗
   │  │  ║ 21 ╟──┤ >> ╟──┘    ├────────────┐  │  ║  str→int helper function                  ║
   │  │  ╚════╝  ╘═╤══╝     ┌─┴─╖  ╔════╗  │  │  ╟───────────────────────────────────────────╢
   │  │  ┌─────────┴─╖    ┌─┤ ≠ ╟──╢ −1 ║  │  │  ║  str→int p(s, k) =                        ║
   │  │  │ str→int p ╟────┤ ╘═══╝  ╚═╤══╝  │  │  ║    let c = s & ((1 << 21) − 1);           ║
   │  │  ╘═╤═════════╝    │        ┌─┴─╖   │  │  ║    let n = (10 × k) + c − '0';            ║
   │  │  ┌─┴─╖  ╔════╗    └────────┤ ? ╟───┘  │  ║    let d = str→int p(s >> 21, n);         ║
   │  └──┤ − ╟──╢ 48 ║             ╘═╤═╝      │  ║    (s = 0 | s = −1) ? k :                 ║
   │     ╘═══╝  ╚════╝             ┌─┴─╖      │  ║    (c < '0' | c > '9' | d = −1) ? −1 : d  ║
   └───────────────────────────────┤ ? ╟──────┘  ╚═══════════════════════════════════════════╝
                                   ╘═╤═╝
                                     │
                                  └──┘

        ╔═══╗  ┌───────────╖    ╓─────────╖    
        ║ 0 ╟──┤ int→str p ╟──┐ ║ int→str ║       ╔═════════════════════════════════════════╗
        ╚═══╝  ╘═════╤═════╝  │ ╙────┬────╜       ║  int→str                                ║
        ┌────────────┴────┐   └──────┴─┐          ╟─────────────────────────────────────────╢
        │  ╔════╗  ┌────╖ │ ╔═══════╗  │          ║  • Converts integers to strings         ║
        │  ║ 21 ╟──┤ << ╟─┘ ║ −8723 ║  │          ║    containing their decimal value       ║
        │  ╚════╝  ╘══╤═╝   ╚╤══════╝  │          ║  • Uses − (minus), not - (hyphen) for   ║
        │            ┌┴┐     │  ╔═══╗  │          ║    negatives                            ║
        │            └┬┘     │  ║ 0 ║  │          ║  • No support for thousands separators  ║
        │             └─┬────┘  ╚═╤═╝  │          ╟─────────────────────────────────────────╢
        │             ┌─┴─╖     ┌─┴─╖  │          ║  int→str(i) =                           ║
        └─────────────┤ ? ╟─────┤ < ║  │          ║    let s = int→str p(i, 0);             ║
                      ╘═╤═╝     ╘═╤═╝  │          ║    i ? (i < 0 ? (s<<21)|'−' : s) : '0'  ║
              ╔════╗  ┌─┴─╖       ├────┘          ╚═════════════════════════════════════════╝
              ║ 48 ╟──┤ ? ╟───────┘            
              ╚════╝  ╘═╤═╝                    
                        │                      

   ┌──────────────────┐  ╓┬───────────╖
   │  ╔════╗  ┌────╖  ├──╫┘ int→str p ╟─────────────┐
   │  ║ 21 ╟──┤ << ╟──┘  ╙────────────╜   ╔════╗    │
   │  ╚════╝  ╘═╤══╝                      ║ 10 ║    │   ╔════════════════════════════════════╗
   │           ┌┴┐                        ╚═╤══╝    │   ║  int→str helper function           ║
   │           └┬┘         ┌───────────╖  ┌─┴──╖    │   ╟────────────────────────────────────╢
   │            ├──────────┤ int→str p ╟──┤ ÷% ╟──┐ │   ║  int→str p(i, k) =                 ║
   │           ┌┴┐         ╘═════╤═════╝  ╘═╤══╝  ├─┘   ║    let (q, r) = i ÷% 10;           ║
   │           └┬┘             ┌─┴─╖        │     │     ║    let n = (k << 21) | (|r|+'0');  ║
   │  ╔════╗  ┌─┴─╖  ┌───╖  ┌──┤ · ╟────────┘     │     ║    i ? int→str(q, n) : k           ║
   │  ║ 48 ╟──┤ + ╟──┤ | ╟──┘  ╘═╤═╝              │     ╚════════════════════════════════════╝
   │  ╚════╝  ╘═══╝  ╘═══╝     ┌─┴─╖              │
   └───────────────────────────┤ ? ╟──────────────┘
                               ╘═╤═╝
                                 │
                              └──┘











