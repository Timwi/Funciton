
                      ╔════╗    ╔═══════════════════════════════════╗            ╔═════════════╗
                      ║ 21 ║    ║  String length                    ║            ║  Is string  ║
                      ╚═╤══╝    ╟───────────────────────────────────╢            ║  non-empty  ║
               ┌───╖  ┌─┴──╖    ║  ℓ(s) = ■(s) ? ♯(ℓ(s >> 21)) : 0  ║            ╚═════════════╝
          ┌────┤ ℓ ╟──┤ >> ║    ╚═══════════════════════════════════╝                 ╓───╖
          │    ╘═══╝  ╘═╤══╝                                                          ║ ■ ║
        ┌─┴─╖     ╓───╖ │                      ╓───╖                                  ╙─┬─╜
        │ ♯ ║     ║ ℓ ║ │                   ┌──╢ ‼ ╟────────────────────────┐       ┌───┴───┐
        ╘═╤═╝     ╙─┬─╜ │                   │  ╙───╜  ┌───╖  ┌───╖  ╔════╗  │     ┌─┴─╖   ┌─┴─╖
  ╔═══╗ ┌─┴─╖ ┌───╖ │   │                ┌──┴─────────┤ ℓ ╟──┤ × ╟──╢ 21 ║  │     │ > ╟─┬─┤ > ║
  ║ 0 ╟─┤ ‽ ╟─┤ ■ ╟─┴───┘                │            ╘═══╝  ╘═╤═╝  ╚════╝  │     ╘═╤═╝ │ ╘═╤═╝
  ╚═══╝ ╘═╤═╝ ╘═══╝                   ┌──┴─────────┐      ┌────┴──┐         │    ╔══╧═╗   ╔═╧═╗
          │                           │   ┌───╖  ┌─┴─╖  ┌─┴──╖    │         │    ║ −2 ║   ║ 1 ║
                                      │ ┌─┤ + ╟──┤ · ╟──┤ << ║    │         │    ╚════╝   ╚═══╝
 ╔═════════════════════════════════╗  └─┤ ╘═╤═╝  ╘═╤═╝  ╘═╤══╝    │         │
 ║  String concatenation           ║    │ ┌─┴─╖  ┌─┴─╖  ╔═╧═╗     │ ┌────╖  │
 ╟─────────────────────────────────╢    └─┤ ‽ ╟──┤ > ║  ║ 1 ║     └─┤ << ╟──┘   ╔═════════════╗
 ║  ‼(a, b) =                      ║      ╘═╤═╝  ╘═╤═╝  ╚═══╝ ╔═══╗ ╘══╤═╝      ║  Is string  ║
 ║     let c = 21 × ℓ(a);          ║       ┌┴┐     └──────────╢ 0 ║   ┌┴┐       ║    empty    ║
 ║     (a < 0 ? a + (1 << c) : a)  ║       └┬┘                ╚═══╝   └┬┘       ╚═════════════╝
 ║         | (b << c)              ║        └──────────────┬───────────┘        ╓───╖ ┌───╖ ┌┐
 ╚═════════════════════════════════╝                       │                    ║ □ ╟─┤ ■ ╟─┤├─
                                                                                ╙───╜ ╘═══╝ └┘

 ┌───────────────────────────────────┐
 │       ┌─                        ┌─┴─╖            ╔═══════════════════════════════════════════╗
 │     ┌─┴──╖  ╔═══╗     ┌─────────┤ · ╟────┐       ║  Find first occurrence of substring       ║
 │   ┌─┤ ʘp ╟──╢ 0 ║     │ ╓┬────╖ ╘═╤═╝    │       ║  (returns −1 if substring not found)      ║
 │   │ ╘═╤══╝  ╚═══╝     └─╫┘ ʘp ╟───┘ ┌────┴───┐   ╟───────────────────────────────────────────╢
 │   │ ╓─┴─╖               ╙──┬──╜   ┌─┴─╖      │   ║  ʘ(h, n) = ʘp(h, n, 0)                    ║
 │   │ ║ ʘ ║    ┌─────────────┴──────┤ · ╟─┐    │   ╟───────────────────────────────────────────╢
 │   │ ╙─┬─╜    │  ╔════╗  ┌────╖    ╘═╤═╝ │    │   ║  ʘp(h, n, a) =                            ║
 │   └───┘      │  ║ 21 ╟──┤ >> ╟──────┘   │    │   ║      let l = 21 × ℓ(n);                   ║
 │              │  ╚════╝  ╘═╤══╝          │    │   ║      let d = h & ¬(−1 << l);              ║
 │     ┌───╖  ┌─┴──╖         │             │    │   ║      (d = n) | (d ^ (−1 << l) = n) ? a :  ║
 │ ┌───┤ ♯ ╟──┤ ʘp ╟─────────┘             │    │   ║      ■(h) ? ʘp(h >> 21, n, a + 1) : −1    ║
 └─┤   ╘═══╝  ╘═╤══╝                       │    │   ╚═══════════════════════════════════════════╝
   │  ╔════╗  ┌─┴─╖  ┌───╖               ┌─┴─╖  │
   │  ║ −1 ╟──┤ ‽ ╟──┤ ■ ╟───────────────┤ · ╟──┴─┐
   │  ╚════╝  ╘═╤═╝  ╘═══╝               ╘═╤═╝  ┌─┴─╖             ╔═════════════════════════════════════════════╗
   │            │                  ┌───────┴────┤ · ╟─────┐       ║  Reverse string                             ║
   │            │                ┌─┴─╖          ╘═╤═╝     │       ╟─────────────────────────────────────────────╢
   │            │             ┌──┤ · ╟┐    ┌┐     │       │       ║  ⮌(s) = □(s) ? s : ⮌(s >> 21) ‼ ʃ(s, 0, 1)  ║
   │            │           ┌─┴─╖╘═╤═╝├────┤├─────┤       │       ╚═════════════════════════════════════════════╝
   │            │         ┌─┤ ≠ ║  │  │    └┘    ┌┴┐    ┌─┴─╖          ╔════╗ ┌────╖     ╓───╖
   │          ┌─┴─╖   ┌┐  │ ╘═╤═╝  │  │          └┬┘    │ ℓ ║          ║ 21 ╟─┤ >> ╟──┐  ║ ⮌ ║
   └──────────┤ ‽ ╟───┤├──┤   ├────┘  │ ╔════╗ ┌──┴─╖   ╘═╤═╝          ╚════╝ ╘═╤══╝  │  ╙─┬─╜
              ╘═╤═╝   └┘  │ ┌─┴─╖     │ ║ −1 ╟─┤ << ╟─┐ ┌─┴─╖                 ┌─┴─╖   ├────┴────┐
                │         └─┤ ≠ ║     │ ╚════╝ ╘════╝ ├─┤ × ║                 │ ⮌ ║   │         │
                            ╘═╤═╝     │               │ ╘═╤═╝                 ╘═╤═╝   │         │
                            ┌─┴─╖     │               │ ╔═╧══╗                ┌─┴─╖ ┌─┴─╖ ┌───╖ │
                      ┌─────┤ ^ ╟─────┘               │ ║ 21 ║                │ ‼ ╟─┤ ‽ ╟─┤ □ ╟─┴─┐
           ╔════╗  ┌──┴─╖   ╘═══╝                     │ ╚════╝                ╘═╤═╝ ╘═╤═╝ ╘═══╝   │
           ║ −1 ╟──┤ << ╟─────────────────────────────┘                       ┌─┴─╖ ╔═══╗ ╔═══╗   │
           ╚════╝  ╘════╝                                                   ┌─┤ ʃ ╟─╢ 1 ║ ║ 0 ║   │
                                                                            │ ╘═╤═╝ ╚═══╝ ╚═╤═╝   │
                                                                            │   └───────────┘     │
                                                                            └─────────────────────┘
 ╔═════════════════════════════╤═════════════╗
 ║  ʃ(s, i, l) =               │  Substring  ║
 ║      let m = 21 × l;        └─────────────╢  ┌──────────────────────────┐
 ║      let b = s >> (21 × i);               ║  │                        ┌─┴─╖
 ║      let f = b >> m;                      ║  │              ┌─────────┤ · ╟──┐
 ║      let c = ((b << 21) >> m) & 2097151;  ║  │              │  ╓───╖  ╘═╤═╝  │
 ║      let r = b & ¬(−1 << m);              ║  │  ┌───╖       └──╢ ʃ ╟────┘    │
 ║      ■(f) ? b : c ? r : (r | (−1 << m))   ║  └──┤ × ╟─────┐    ╙─┬─╜         │
 ╚═══════════════════════════════════════════╝     ╘═╤═╝  ╔══╧═╗  ┌─┴─╖  ┌────╖ │
                         ┌───────────────────────────┴─┐  ║ 21 ║  │ × ╟──┤ >> ╟─┘
             ┌───────────┴───────────┐                 │  ╚══╤═╝  ╘═╤═╝  ╘═╤══╝
             │  ┌────╖  ╔════╗       │               ┌─┴─╖   └──────┘      │
     ┌───────┴──┤ << ╟──╢ −1 ║     ┌─┴─╖    ┌────────┤ · ╟──┐              │
     │          ╘══╤═╝  ╚════╝  ┌──┤ · ╟────┴─────┐  ╘═╤═╝  ├──────────────┘
     │           ┌─┴─┐        ┌─┘  ╘═╤═╝  ╔════╗  │    │    │
     │           │   ├────┬───┘      │    ║ 21 ║  │    │    │
     │           └───┘   ┌┴┐    ┌────┘    ╚══╤═╝  │    │    │
     │  ┌────╖  ╔════╗   └┬┘    │ ┌────╖  ┌──┴─╖  │    │    │
     └──┤ << ╟──╢ −1 ║  ┌─┴──┐  └─┤ >> ╟──┤ << ║  │    │    │
        ╘═╤══╝  ╚════╝ ┌┴┐   │    ╘═╤══╝  ╘══╤═╝  │    │    │
         ┌┴┐           └┬┘ ┌─┴─╖ ┌┐ │        └────┘    │    │
         └┬┘            ├──┤ ? ╟─┤├─┤  ╔═════════╗     │    │
          └─────────────┘  ╘═╤═╝ └┘ └──╢ 2097151 ║     │    │
                           ┌─┴─╖       ╚═════════╝   ┌─┴──╖ │
                        ┌──┤ ‽ ╟───┐      ┌───╖    ┌─┤ >> ║ │
                        │  ╘═╤═╝   └──────┤ ■ ╟────┘ ╘═╤══╝ │
                        │    │            ╘═══╝        ├────┘
                        └──────────────────────────────┘

                                                        ╔═════════════════════════════════╗
           ╔════════════════════════════════╗           ║  Repeat string (helper)         ║
           ║  Repeat string                 ║           ╟─────────────────────────────────╢
           ╟────────────────────────────────╢           ║  …p(s, l, n) =                  ║
           ║  …(s, n) = n > 0               ║           ║    n ? (…p(s, l, ♭(n)) << l) |  ║
           ║     ? …p(s, 21 × ℓ(s), n) : 0  ║           ║        (s & ¬(−1 << l))         ║
           ╚════════════════════════════════╝           ║      : 0                        ║
                                                        ╚═════════════════════════════════╝

            ╔════╗  ┌───╖  ┌───╖                     ┌────────────────────────────────┐
            ║ 21 ╟──┤ × ╟──┤ ℓ ╟──┐                  │                       ╓┬────╖  │
            ╚════╝  ╘═╤═╝  ╘═══╝  │  ╓───╖           │                   ┌───╫┘ …p ╟──┘   ┌────╖  ╔════╗
                    ┌─┴──╖        ├──╢ … ╟─┐         │                 ┌─┴─╖ ╙──┬──╜   ┌──┤ << ╟──╢ −1 ║
                 ┌──┤ …p ╟────────┘  ╙───╜ │         │             ┌───┤ · ╟────┴──────┘  ╘══╤═╝  ╚════╝
                 │  ╘═╤══╝                 │         │         ┌───┴──┐╘═╤═╝                ┌┴┐
                 │  ┌─┴─╖                  │         │ ┌───╖ ┌─┴─╖ ┌──┴─╖└─────────┐        └┬┘
                 └──┤ · ╟────┐             │       ┌─┴─┤ ♭ ╟─┤ · ╟─┤ …p ╟──────────┴───┐   ┌─┴─╖
                    ╘═╤═╝    ├─────────────┘       │   ╘═══╝ ╘═╤═╝ ╘══╤═╝              └───┤ · ╟─┐
             ╔═══╗  ┌─┴─╖  ┌─┴─╖                   │           │   ┌──┴─╖        ┌──┐      ╘═╤═╝ │
             ║ 0 ╟──┤ ‽ ╟──┤ < ║                   │           │   │ << ╟────────┤  ├───┬────┤   │
             ╚═╤═╝  ╘═╤═╝  ╘═╤═╝                   │           │   ╘══╤═╝  ╔═══╗ └──┘ ┌─┴─╖  └───┘
               │      │      │                     │           └──────┘    ║ 0 ╟──────┤ ? ╟──┐
               │             │                     │                       ╚═══╝      ╘═╤═╝  │
               └─────────────┘                     │                                    │    │
                                                   │                                         │
                                                   └─────────────────────────────────────────┘


                            ╓───╖          ╔═════════════════════════════════════════════════════╗         ╔═════════════════════════════╗
                            ║ 《 ║          ║  String to integer conversion                       ║         ║  Deprecated function names  ║
     ╔═════════╗            ╙─┬─╜          ╟─────────────────────────────────────────────────────╢         ║ for backwards compatibility ║
     ║ 2097151 ╟────┬─────────┴─────────┐  ║  • Converts numeric strings to their integer value  ║         ╚═════════════════════════════╝
     ╚═════════╝   ┌┴┐                  │  ║  • Accepts − (minus) or - (hyphen) for negative     ║            ╓─────────╖  ╓─────────╖
     ╔════╗  ┌───╖ └┬┘ ┌───╖  ╔══════╗  │  ║  • Returns 0 if the string is not an integer        ║            ║ str→int ║  ║ int→str ║
     ║ 45 ╟──┤ ≠ ╟──┴──┤ ≠ ╟──╢ 8722 ║  │  ║  • No tolerance for leading/trailing whitespace     ║            ╙────┬────╜  ╙────┬────╜
     ╚════╝  ╘═╤═╝     ╘═╤═╝  ╚══════╝  │  ║  • No support for thousands separators              ║               ┌─┴─╖        ┌─┴─╖
               └────┬────┘            ┌─┴─╖╚═════════════════════════════════════════════════════╝               │ 《 ║        │ 》 ║
    ┌───────────────┴─────────────────┤ · ╟──────────────────────┐                                               ╘═╤═╝        ╘═╤═╝
    │  ╔═════════════════════════════╗╘═╤═╝                      │     ╔════════════════════════════════════╗      │            │
    │  ║ 《(s) =                      ║  │  ┌──────────────────┐  │     ║  Convert string into lazy          ║
    │  ║  let c = s & (1<<21 − 1);   ║  │  │  ╔════╗  ┌────╖  │  │     ║  sequence of its characters        ║
    │  ║  let m = c = '-' | c = '−'; ║  └──┤  ║ 21 ╟──┤ >> ╟──┘  │     ╟────────────────────────────────────╢
    │  ║  let w = m ? s >> 21 : s;   ║     │  ╚════╝  ╘═╤══╝     │     ║  ↯(s) = □(s) ? 0 :                 ║
    │  ║  let t = 《p(w, 0);          ║     │          ┌─┴─╖      │     ║    •[s & ¬(−1 << 21), ↯(s >> 21)]  ║
    │  ║  t = -1 ? 0 : m ? ~t : t    ║     └──────────┤ ? ╟──────┘     ╚════════════════════════════════════╝
    │  ╚═════════════════════════════╝                ╘═╤═╝                               ╓───╖
    └────────┐    ╔═══╗                   ┌────╖        │                                 ║ ↯ ║
             │    ║ 0 ╟───────────────────┤ 《p ╟────────┘                                 ╙─┬─╜
             │    ╚═╤═╝    ┌──────┐       ╘═╤══╝                                 ┌──────────┴─────────┐
           ┌─┴─╖  ┌─┴─╖  ┌─┴─╖  ╔═╧══╗      │                                    │            ╔═══╗ ┌─┴─╖
        ┌──┤ ‽ ╟──┤ ‽ ╟──┤ = ║  ║ −1 ║      │                                    │      ┌─┐   ║ 0 ║ │ □ ║
      ┌─┴─╖╘═╤═╝  ╘═╤═╝  ╘═╤═╝  ╚════╝      │                                    │      └─┤   ╚═╤═╝ ╘═╤═╝
      │ ~ ║  │      │      ├────────────────┘                             ┌──────┴─┐ ┌┐ ╔═╧═╕ ┌─┴─╖   │
      ╘═╤═╝  ├─────────────┘                                              │        ├─┤├─╢   ├─┤ ‽ ╟───┘
        └────┘                                                            │        │ └┘ ╚═╤═╛ ╘═╤═╝
                                                                          │ ╔══════╧══╗ ┌─┴─╖   │
            ╓┬────╖                                                       │ ║ 2097151 ║ │ ↯ ║
      ┌─────╫┘ 《p ╟────────────────────┐                                  │ ╚═════════╝ ╘═╤═╝
      │     ╙─────╜      ┌─────────────┴─────────────┐                    │             ┌─┴──╖ ╔════╗
      │                  │      ╔═════════╗        ┌─┴─╖                  └─────────────┤ >> ╟─╢ 21 ║
      │               ┌──┴───┬──╢ 2097151 ║        │ ■ ║                                ╘════╝ ╚════╝
   ┌──┴─┐             │     ┌┴┐ ╚═════════╝ ╔════╗ ╘═╤═╝
   │  ┌─┴─╖  ┌───╖  ┌─┴─╖   └┬┘             ║ 57 ╟─┐ │
   │  │ × ╟──┤ + ╟──┤ · ╟────┴───────────┐  ╚════╝ │ │
   │  ╘═╤═╝  ╘═╤═╝  ╘═╤═╝ ╔════╗  ┌───╖  │  ┌───╖  │ │
   │  ╔═╧══╗   │      │   ║ 48 ╟──┤ ≥ ╟──┴──┤ ≥ ╟──┘ │
   │  ║ 10 ║   │      └┐  ╚════╝  ╘═╤═╝     ╘═╤═╝    │
   │  ╚════╝   │       └┐   ┌───┐   └────┬────┘      │
   │  ┌────────┘        └┐  │   ├────────┘    ┌──────┘
   │  │  ╔════╗  ┌────╖  │  └─┬─┘             │  ╔══════════════════════════════════════════════════╗
   │  │  ║ 21 ╟──┤ >> ╟──┘    ├────────────┐  │  ║  《p (string-to-integer helper function)          ║
   │  │  ╚════╝  ╘═╤══╝     ┌─┴─╖  ╔════╗  │  │  ╟──────────────────────────────────────────────────╢
   │  │  ┌─────────┴─╖    ┌─┤ ≠ ╟──╢ −1 ║  │  │  ║  《p(s, k) =                                      ║
   │  │  │    《p     ╟────┤ ╘═══╝  ╚══╤═╝  │  │  ║    let c = s & ((1 << 21) − 1);                  ║
   │  │  ╘═╤═════════╝    │         ┌─┴─╖  │  │  ║    let n = (10 × k) + c − '0';                   ║
   │  │  ┌─┴─╖  ╔════╗    └─────────┤ ? ╟──┘  │  ║    let d = 《p(s >> 21, n);                       ║
   │  └──┤ − ╟──╢ 48 ║              ╘═╤═╝     │  ║    ■(s) ? (c < '0' | c > '9' | d = −1) ? −1 : d  ║
   │     ╘═══╝  ╚════╝              ┌─┴─╖     │  ║         : k                                      ║
   └────────────────────────────────┤ ‽ ╟─────┘  ╚══════════════════════════════════════════════════╝
                                    ╘═╤═╝
                                      │
                                   └──┘

             ╔═══╗  ┌────╖      ╓───╖
             ║ 0 ╟──┤ 》p ╟──┐   ║ 》 ║             ╔═════════════════════════════════════════╗
             ╚═══╝  ╘══╤═╝  │   ╙─┬─╜             ║  Integer to string conversion           ║
        ┌──────────────┴──┐ └─────┴────┐          ╟─────────────────────────────────────────╢
        │  ╔════╗  ┌────╖ │ ╔═══════╗  │          ║  • Converts integers to strings         ║
        │  ║ 21 ╟──┤ << ╟─┘ ║ −8723 ║  │          ║    containing their decimal value       ║
        │  ╚════╝  ╘══╤═╝   ╚╤══════╝  │          ║  • Uses − (minus), not - (hyphen) for   ║
        │            ┌┴┐     │  ╔═══╗  │          ║    negatives                            ║
        │            └┬┘     │  ║ 0 ║  │          ║  • No support for thousands separators  ║
        │             └─┬────┘  ╚═╤═╝  │          ╟─────────────────────────────────────────╢
        │             ┌─┴─╖     ┌─┴─╖  │          ║  》(i) =                                 ║
        └─────────────┤ ? ╟─────┤ < ║  │          ║    let s = 》p(i, 0);                    ║
                      ╘═╤═╝     ╘═╤═╝  │          ║    i ? (i < 0 ? (s<<21)|'−' : s) : '0'  ║
              ╔════╗  ┌─┴─╖       ├────┘          ╚═════════════════════════════════════════╝
              ║ 48 ╟──┤ ? ╟───────┘
              ╚════╝  ╘═╤═╝
                        │

   ┌──────────────────┐  ╓┬────╖
   │  ╔════╗  ┌────╖  ├──╫┘ 》p ╟────────────┐
   │  ║ 21 ╟──┤ << ╟──┘  ╙─────╜  ╔════╗    │
   │  ╚════╝  ╘═╤══╝              ║ 10 ║    │   ╔══════════════════════════════════════════╗
   │           ┌┴┐                ╚═╤══╝    │   ║  》p (integer-to-string helper function)  ║
   │           └┬┘         ┌────╖ ┌─┴──╖    │   ╟──────────────────────────────────────────╢
   │            ├──────────┤ 》p ╟─┤ ÷% ╟──┐ │   ║  》p(i, k) =                              ║
   │           ┌┴┐         ╘═╤══╝ ╘═╤══╝  ├─┘   ║     let (q, r) = i ÷% 10;                ║
   │           └┬┘           │      │     │     ║     let n = (k << 21) | (|r|+'0');       ║
   │  ╔════╗  ┌─┴─╖  ┌───╖ ┌─┴─╖    │     │     ║     i ? 》p(q, n) : k                     ║
   │  ║ 48 ╟──┤ + ╟──┤ | ╟─┤ · ╟────┘     │     ╚══════════════════════════════════════════╝
   │  ╚════╝  ╘═══╝  ╘═══╝ ╘═╤═╝          │
   │                       ┌─┴─╖          │
   └───────────────────────┤ ? ╟──────────┘
                           ╘═╤═╝
                             │
                          └──┘

